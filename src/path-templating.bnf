; OpenAPI Path Templating ABNF syntax
path-template                  = path [ query-marker query ]
path                           = slash *( path-segment slash ) [ path-segment ]
path-segment                   = 1*( path-literal / template-expression )
query                          = *( query-literal / template-expression )
query-literal                  = 1*( unreserved / pct-encoded / sub-delims / ":" / "@" / "/" / "?" / "&" / "=" )
query-marker                   = "?"
slash                          = "/"
path-literal                   = 1*( unreserved / pct-encoded / sub-delims-no-slash / ":" / "@" )
template-expression            = "{" template-expression-param-name "}"
template-expression-param-name = 1*( unreserved / pct-encoded / sub-delims-no-slash / ":" / "@" )

; Characters definitions (from RFC 3986)
unreserved          = ALPHA / DIGIT / "-" / "." / "_" / "~"
pct-encoded         = "%" HEXDIG HEXDIG
sub-delims          = "!" / "$" / "&" / "'" / "(" / ")"
                    / "*" / "+" / "," / ";" / "="
sub-delims-no-slash = "!" / "$" / "&" / "'" / "(" / ")"
                    / "*" / "+" / "," / ";"
ALPHA               = %x41-5A / %x61-7A   ; A-Z / a-z
DIGIT               = %x30-39            ; 0-9
HEXDIG              = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
